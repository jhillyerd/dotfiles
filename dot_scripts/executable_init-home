#!/usr/bin/env fish
# init-home: initialize my home directory from repos

function STAGE -a text
  set_color -r
  echo "### $text"
  set_color normal
end

function checkout_repo -a url path
  if [ -d $path ]
    echo "$path already exists!"
    exit 1
  end

  git clone -- $url $path; or exit 1
end

function checkout_my_repo -a name path
  # Checkout via HTTPS.
  set repo "jhillyerd/$name"
  set url "https://github.com/jhillyerd/$name"
  checkout_repo $url $path

  # Update to use SSH remote.
  set name_url origin "git@github.com:"$repo".git"
  cd $path
  git remote set-url $name_url 2>/dev/null; or git remote add $name_url
  or exit 1
end

# Verify necessary tools are installed.
for cmd in git chezmoi sha256sum
  if ! type -qf $cmd
    echo "missing $cmd"
    exit 1
  end
end

set root (pwd)
set tempdir (mktemp -d)
if test -z $tempdir -o ! -d $tempdir
  echo "failed to create tempdir"
  exit 1
end

STAGE "Fetch and verify oh-my-fish installer"
set want_sum "1464a0163257e729579735955a834ccff4c3eb2e91d015e96e459382acaab2b7  install"
cd $tempdir
curl \
  https://raw.githubusercontent.com/oh-my-fish/oh-my-fish/master/bin/install \
  > install
if ! echo $want_sum | sha256sum -c
  echo "oh-my-fish installer checksum mismatch"
  echo "expected: $want_sum"
  echo "actual:  " (sha256sum install)
  exit 1
end
cd $root

STAGE "Checkout config repos"
checkout_my_repo "dotfiles" "$root/.local/share/chezmoi"
checkout_my_repo "dotvim" "$root/.config/nvim"

STAGE "Setup chezmoi"
set chezdir "$root/.config/chezmoi"
set cheztoml "$chezdir/chezmoi.toml"
if [ ! -e $cheztoml ]
  mkdir -p $chezdir
  echo -n > $cheztoml "\
[data]
  freenode_user = \"jhillyerd\"
  freenode_password = \"SETME\"
  machine = \"desktop\"
  place = \"home\"
  wired = \"eth0\"
  wifi = \"none\"
  wm = \"i3\"
"
end

chezmoi apply; or exit 1

STAGE "Install oh my fish"
fish $tempdir/install --noninteractive
rm -rf $tempdir

STAGE "Home init complete!"
